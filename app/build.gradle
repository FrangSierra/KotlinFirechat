buildscript {
    ext {
        firebase_version = "11.2.0"
        gson_version = "2.8.0"
        dagger_version = "2.11"
        rx_version = "1.2.0"
        spek_version = '1.1.0'
        glide = "3.7.0"
        rx_firebase_version = "1.1.3"
        support_version = "26.0.1"
        rx_android_version = "2.0.1"
        target_sdk_version = 25
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "frangsierra.kotlinfirechat"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility "1.7"
        targetCompatibility "1.7"
    }

    lintOptions {
        abortOnError false
    }

    configurations.all {
        //This is library is included with two different versions
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Anko Commons
    compile "org.jetbrains.anko:anko-commons:$anko_version"

    //Support
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"

    //Glide
    compile "com.github.bumptech.glide:glide:$glide"

    //Firebase
    compile "com.google.firebase:firebase-storage:$firebase_version"
    compile "com.google.firebase:firebase-messaging:$firebase_version"
    compile "com.google.firebase:firebase-common:$firebase_version"
    compile "com.google.firebase:firebase-auth:$firebase_version"
    compile "com.google.firebase:firebase-perf:$firebase_version"
    compile "com.google.firebase:firebase-core:$firebase_version"
    compile "com.google.firebase:firebase-database:$firebase_version"
    compile "com.google.firebase:firebase-crash:$firebase_version"
    compile "com.github.frangsierra:rx2firebase:$rx_firebase_version"

    //Dagger
    compile "com.google.dagger:dagger-android:$dagger_version"
    compile "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    //Rx
    compile "io.reactivex.rxjava2:rxjava:$rx_version"
    compile "io.reactivex.rxjava2:rxandroid:$rx_android_version"
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    //Google sign in
    compile "com.google.android.gms:play-services-auth:$firebase_version"

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    //Test
    testCompile 'junit:junit:4.12'
    testCompile 'com.natpryce:hamkrest:1.3.0.0'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile 'org.junit.platform:junit-platform-runner:1.0.0-M3'
    testCompile("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}


apply plugin: 'com.google.gms.google-services'